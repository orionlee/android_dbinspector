apply plugin: 'com.android.library'
apply from: '../config/quality.gradle'

version = '3.4.2-SNAPSHOT'
group = 'im.dino'

android {
    compileSdkVersion rootProject.ext.compile_sdk

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion rootProject.ext.target_sdk
        versionCode 1
        versionName version

        buildConfigField 'String', 'AUTHORITY_PREFIX', '"dbInspector_"'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    resourcePrefix 'dbinspector_'

    lintOptions {
        abortOnError false
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$support_lib"
    implementation 'commons-io:commons-io:2.4'
}

apply plugin: 'maven'
apply plugin: 'signing'

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.project {
            name 'Android DbInspector'
            packaging 'aar'
            description 'Android library for viewing in app databases.'
            url 'https://github.com/infinum/android_dbinspector/'

            scm {
                url 'scm:git@github.com:infinum/android_dbinspector.git'
                connection 'scm:git@github.com:infinum/android_dbinspector.git'
                developerConnection 'scm:git@github.com:infinum/android_dbinspector.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'reisub'
                    name 'Dino Kovaƒç'
                }
            }
        }
    }
}
